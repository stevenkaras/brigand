buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.+'
    }
}
apply plugin: 'android-library'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 18
    buildToolsVersion "18.1.0"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 18
    }
}

apply plugin: 'maven'
apply plugin: 'signing'

version = "1.1.2"
group = "com.getout-tlv.brigand"

configurations {
    archives {
        extendsFrom configurations.default
    }
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: nexusStageRepo) {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }

                snapshotRepository(url: nexusSnapshotRepo) {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }

                pom.artifactId = 'Brigand'

                pom.project {

                    packaging 'aar'
                    name 'Brigand'
                    description 'Opportunistic bastard of communication libraries'
                    url 'https://stevenkaras.github.io/brigand'

                    scm {
                        url 'scm:git@github.com:stevenkaras/brigand.git'
                        connection 'scm:git@github.com:stevenkaras/brigand.git'
                        developerConnection 'scm:git@github.com:stevenkaras/brigand.git'
                    }

                    licenses {
                        license {
                            name 'MIT Licence'
                            url 'http://opensource.org/licenses/MIT'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'stevenkaras'
                            name 'Steven Karas'
                            email 'steven.karas@gmail.com'
                        }
                    }
                }
            }
        }
    }

    signing {
        required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    task androidJavadocs(type: Javadoc) {
        source = android.sourceSets.main.allJava
    }

    task androidJavadocsJar(type: Jar) {
        classifier = 'javadoc'
        from androidJavadocs.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.allSource
    }

    artifacts {
        archives androidSourcesJar
        archives androidJavadocsJar
    }
}